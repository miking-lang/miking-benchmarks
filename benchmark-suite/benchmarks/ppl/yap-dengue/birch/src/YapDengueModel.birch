/**
 * Model for Yap case study.
 */
final class YapDengueModel < Model {
  /**
   * Human population.
   */
  h:SEIRComponent;

  /**
   * Mosquito population.
   */
  m:SEIRComponent;

  /**
   * Probability of a human case being observed.
   */
  ρ:Real;

  /**
   * Latent aggregate number of cases since last observation.
   */
  z:Integer <- 0;

  /**
   * Observed aggregate number of cases since last observation.
   */
  y:Integer?;

  override function simulate() {
    h.ν <- 0.0;
    h.μ <- 1.0;
    h.λ <- simulate_beta(1.0, 1.0);
    h.δ <- simulate_beta(1.0 + 2.0/4.4, 3.0 - 2.0/4.4);
    h.γ <- simulate_beta(1.0 + 2.0/4.5, 3.0 - 2.0/4.5);

    m.ν <- 1.0/7.0;
    m.μ <- 6.0/7.0;
    m.λ <- simulate_beta(1.0, 1.0);
    m.δ <- simulate_beta(1.0 + 2.0/6.5, 3.0 - 2.0/6.5);
    m.γ <- 0.0;

    ρ <- simulate_beta(1.0, 1.0);
    z <- 0;
    y <- nil;
  }

  override function simulate(t:Integer) {
    if t == 1 {
      /* initial state */
      let n <- 7370;
      h.i[t] <- simulate_poisson(5.0);
      h.i[t] <- h.i[t] + 1;
      h.e[t] <- simulate_poisson(5.0);
      h.r[t] <- simulate_uniform_int(0, n - h.i[t] - h.e[t]);
      h.s[t] <- n - h.e[t] - h.i[t] - h.r[t];

      h.Δs[t] <- 0;
      h.Δe[t] <- h.e[t];
      h.Δi[t] <- h.i[t];
      h.Δr[t] <- 0;

      let u <- simulate_uniform(-1.0, 2.0);
      m.s[t] <- n*pow(10.0, u);
      m.e[t] <- 0;
      m.i[t] <- 0;
      m.r[t] <- 0;

      m.Δs[t] <- 0;
      m.Δe[t] <- 0;
      m.Δi[t] <- 0;
      m.Δr[t] <- 0;
    } else {
      n:Real <- h.s[t - 1] + h.e[t - 1] + h.i[t - 1] + h.r[t - 1];

      /* transition of human population */
      let τ_h <- simulate_binomial(h.s[t - 1], 1.0 - exp(-m.i[t - 1]/n));
      h.transfer(t, τ_h);

      /* transition of mosquito population */
      let τ_m <- simulate_binomial(m.s[t - 1], 1.0 - exp(-h.i[t - 1]/n));
      m.transfer(t, τ_m);
    }

    /* observation */
    z <- z + h.Δi[t];
    if y? {
      if y! > z {
        factor -inf;
      } else {
        factor logpdf_binomial(y!, z, ρ);
      }
      z <- 0;
    }
  }

  override function read(buffer:Buffer) {
  }

  override function write(buffer:Buffer) {
  }

  override function read(t:Integer, buffer:Buffer) {
    y <- buffer.get<Integer>();
  }
}
