// Based on the sample from the standard library.
program infer(nparticles:Integer?,
    nsamples:Integer?, output:String?, quiet:Boolean <- true) {
  global.seed();

  let configBuffer <- slurp("config/config.yaml");

  inputBuffer:Buffer;
  inputBuffer.setEmptyArray();
  let inputPath <- configBuffer.get<String>("input");
  let inputReader <- make_reader(inputPath!);
  inputBuffer <- inputReader.slurp();
  let nsteps <- inputBuffer.size() - 1;
  inputReader.close();

  modelBuffer:Buffer;
  modelBuffer <-? configBuffer.get("model");
  let theModel <- make<Model>(modelBuffer);
  theModel!.read(inputBuffer);

  samplerBuffer:Buffer;
  samplerBuffer <-? configBuffer.get("sampler");
  if !samplerBuffer.get("class")? {
    samplerBuffer.set("class", "ParticleSampler");
  }
  let theSampler <- make<ParticleSampler>(samplerBuffer);

  filterBuffer:Buffer;
  filterBuffer <-? configBuffer.get("filter");
  if !filterBuffer.get("class")? {
    filterBuffer.set("class", "ParticleFilter");
  }
  if nparticles? {
    filterBuffer.set("nparticles", nparticles!);
  }
  let theFilter <- make<ParticleFilter>(filterBuffer);

  if !nsamples? {
    nsamples <-? configBuffer.get<Integer>("nsamples");
    if !nsamples? {
      nsamples <- 1;
    }
  }

  outputWriter:Writer?;
  if output? && output! != "" {
    outputWriter <- make_writer(output!);
  } else {
    writer:JSONStdOutWriter;
    writer.open();
    outputWriter <- writer;
  }

  bar:ProgressBar;
  if !quiet {
    bar.update(0.0);
  }

  buffer:Buffer;
  for n in 1..nsamples! {
    let inputIter <- inputBuffer.walk();
    if inputIter.hasNext() {
      buffer <- inputIter.next();
    } else {
      buffer <- make_buffer();
    }
    theSampler!.sample(theFilter!, theModel!, buffer);
    if !quiet {
      bar.update((n - 1.0)/nsamples! + 1.0/(nsamples!*(nsteps + 1.0)));
    }
    for t in 1..nsteps {
      if inputIter.hasNext() {
        buffer <- inputIter.next();
      } else {
        buffer <- make_buffer();
      }
      theSampler!.sample(theFilter!, t, buffer);
      if !quiet {
        bar.update((n - 1.0)/nsamples! + (t + 1.0)/(nsamples!*(nsteps + 1.0)));
      }
    }
    if outputWriter? {
      x:Model?;
      w:Real;
      theFilter!.b <- ancestor(theFilter!.w);
      (x, w) <- theSampler!.draw(theFilter!);
      outputBuffer:Buffer;
      outputBuffer.set("lweight", w);
      outputBuffer.push("sample", x!);
      outputWriter!.push(outputBuffer);
      outputWriter!.flush();
    }
    if !quiet {
      bar.update(scalar<Real>(n)/nsamples!);
    }
  }

  if outputWriter? {
    outputWriter!.close();
  }
}
