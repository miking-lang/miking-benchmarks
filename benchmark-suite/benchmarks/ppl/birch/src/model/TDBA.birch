class TDBA < BirthDeathModel<PhylogenyNode, TDBDParameter> {
  λ_k:Real;
  λ_θ:Real;
  z_σ2:Real;
  ρ:Real;

  override function init() {
    θ.λ ~ Gamma(λ_k, λ_θ);
    θ.μ <- 0.0;
    θ.z ~ Gaussian(0.0, z_σ2);
  }

  override function step() {
    let ξ <- tree.getAge();
    let λ_beg <- θ.λ.value()*exp(θ.z.value()*(ξ - ν.t - ν.Δ));
    let λ_end <- θ.λ.value()*exp(θ.z.value()*(ξ - ν.t));
    let λ_0 <- θ.λ.value()*exp(θ.z.value()*ξ);
    factor -(λ_end - λ_beg)/θ.z.value()
        - 2*log(ρ - (ρ - 1)*exp((λ_beg - λ_0)/θ.z.value()))
        + 2*log(ρ - (ρ - 1)*exp((λ_end - λ_0)/θ.z.value()));
    if ν.isSpeciation() {
      0.0 ~> Exponential(λ_end);
    }
    if ν.isLeaf() {
      true ~> Bernoulli(ρ);
    }
  }

  override function correctForSurvivorshipBias() {
    let λ_0 <- θ.λ.value()*exp(θ.z.value()*tree.getAge());
    factor 2*log(1 - (ρ - 1)/ρ*exp((θ.λ.value() - λ_0)/θ.z));
  }

  override function read(buffer:Buffer) {
    super.read(buffer);
    λ_k <-? buffer.get<Real>("λ_k");
    λ_θ <-? buffer.get<Real>("λ_θ");
    z_σ2 <-? buffer.get<Real>("z_σ2");
    ρ <-? buffer.get<Real>("ρ");
  }
}
